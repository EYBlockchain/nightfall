import "hashes/sha256/512bit.code" as sha256compression
import "utils/pack/unpack128.code" as unpack128

def CONCAT128x216x216(field[128] a, field[216] b, field[216] c)->(field[560]):
	return [...a, ...b, ...c]

def CONCAT216x216(field[216] a, field[216] b)->(field[432]):
	return [...a, ...b]

def ORDEREDCONCAT216x216(field order, field[216] f, field[216] g)->(field[432]):
	field[216] r = if order==0 then g else f fi
	field[216] s = if order==0 then f else g fi
	return [...r, ...s]

def shaPad432To512(field[432] a)->(field[512]):
	return [...a, 1, 70....0, 1,1,0,1,1,0,0,0,0]

def shaPad216To512(field[216] a)->(field[512]):
	return [...a, 1, 287....0, 1,1,0,1,1,0,0,0]

def shaPad128To512(field[128] a)->(field[512]):
	return [...a, 1, 375....0, 1,0,0,0,0,0,0,0]


//inputs for main are:
//Alice's original coins are C and D

// C, the amount contained in coin z_C (private)
// SkA Alice's secret key (private)
// Sc the serial number for coin z_C (private)
// c0b-c7b - the Merkle path for C (private)

// D, the amount contained in coin z_d (private)
// Sd the serial number for coin z_d (private)
// d0b-d7b - the Merkle path for D (private)

//Nc the nullifier for C (public)
//Nd the nullifier for D (public)

// Alice's new coins
// E, the amount in coin z_e (private)
// PkB Bob's public key (private)
// Se the serial number for coin E (private)
// Ze the z-coin (commitment) corresponding to E (public)

// F, the amount in coin f_e (private)
// Sf the serial number for coin F (private)
// Zf the z-coin (commitment) corresponding to F (public)

// R - the Merkle root (public)

// Note - We truncate the 256 bit SHA hash down to 216 bits to make it fit better with our sha, which can only handle a single round.

//Each field element passed to main() can be at most 128bits, to make use of unpack128() so 216 bit variables span two fields

def main(private field pC, 'field' £pSkA1...0, 'field' £pSc1...0, @[private field c*bh, private field c*bl]0...31, private field pOrderC, private field pD, 'field' £pSd1...0, @[private field d*bh, private field d*bl]0...31, private field pOrderD, 'field' pNc1...0, 'field' pNd1...0, private field pE, 'field' £pPkB1...0, 'field' £pSe1...0, 'field' pZe1...0, private field pF, 'field' £pSf1...0, 'field' pZf1...0, 'field' pR1...0)->(field1):

//Unpack the inputs of main() to 128 bits. We'll unpack each field to its own 128 bit string for simplicity for now. Later efficiencies could be made by grouping some inputs.

	field[128] C = unpack128(pC)

	field[128] SkA1 = unpack128(pSkA1)
	field[128] SkA0 = unpack128(pSkA0)
	field[256] SkA = [...SkA1, ...SkA0]

	field[128] Sc1 = unpack128(pSc1) //this slightly long-winded approach needed so Zokrates can flatten the expressions
	field[128] Sc0 = unpack128(pSc0)
	field[256] Sc = [...Sc1, ...Sc0]

	{field[128] c*b1 = unpack128(c*bh)}31...0
	{field[128] c*b0 = unpack128(c*bl)}31...0
	{field[256] c*b = [...c*b1, ...c*b0]}31...0

	field[128] D = unpack128(pD)

	field[128] Sd1 = unpack128(pSd1)
	field[128] Sd0 = unpack128(pSd0)
	field[256] Sd = [...Sd1, ...Sd0]

	{field[128] d*b1 = unpack128(d*bh)}31...0
	{field[128] d*b0 = unpack128(d*bl)}31...0
	{field[256] d*b = [...d*b1, ...d*b0]}31...0

	field[128] Nc1 = unpack128(pNc1)
	field[128] Nc0 = unpack128(pNc0)
	field[256] Nc = [...Nc1, ...Nc0]

	field[128] Nd1 = unpack128(pNd1)
	field[128] Nd0 = unpack128(pNd0)
	field[256] Nd = [...Nd1, ...Nd0]

	field[128] E = unpack128(pE)

	field[128] PkB1 = unpack128(pPkB1)
	field[128] PkB0 = unpack128(pPkB0)
	field[256] PkB = [...PkB1, ...PkB0]

	field[128] Se1 = unpack128(pSe1)
	field[128] Se0 = unpack128(pSe0)
	field[256] Se = [...Se1, ...Se0]

	field[128] Ze1 = unpack128(pZe1)
	field[128] Ze0 = unpack128(pZe0)
	field[256] Ze = [...Ze1, ...Ze0]

	field[128] F = unpack128(pF)

	field[128] Sf1 = unpack128(pSf1)
	field[128] Sf0 = unpack128(pSf0)
	field[256] Sf = [...Sf1,...Sf0]

	field[128] Zf1 = unpack128(pZf1)
	field[128] Zf0 = unpack128(pZf0)
	field[256] Zf = [...Zf1, ...Zf0]

	field[128] R1 = unpack128(pR1)
	field[128] R0 = unpack128(pR0)
	field[256] R = [...R1, ...R0]

	field[128] orderC = unpack128(pOrderC)
	field[128] orderD = unpack128(pOrderD)

//*************first we construct a proof that the nullifiers Nc and Nd are correct************
	//concatenate Sc|Sk. The padding needs to be added to create a true hash

		field[432] t = CONCAT216x216(Sc[40..256], SkA[40..256])
		field[512] u = shaPad432To512(t)
		field[256] nc = sha256compression(u[0..256],u[256..512])

	//concatenate Sd|Sk. The padding needs to be added to create a true hash

		t = CONCAT216x216(Sd[40..256], SkA[40..256])
		u = shaPad432To512(t)
		field[256] nd = sha256compression(u[0..256],u[256..512])

// *********next, calculate Alice's public key from her private key: PkA = H(SkA) truncated to 216 bits.  This will be useful later

		u = shaPad216To512(SkA[40..256])
		field[256] PkA = sha256compression(u[0..256],u[256..512])

//*************Prove that Zc and Zd are in the Merkle tree

	// ***First calculate Zc = H(C|PkA|S)

		field[560] d = CONCAT128x216x216(C, PkA[40..256], Sc[40..256])
		//this is too big to handle in a single sha round and our sha can't handle multiple rounds
		//as a work-around, we will split d, hash both parts and then hash the result - remember to do this in Node too!
		//first 432 bits (432 is 216*2 - a convenient number in our 216 bit scheme)
		field[512] q = shaPad432To512(d[128..560])
		field[256] s = sha256compression(q[0..256],q[256..512])
		//remaining 128 bits
		u = shaPad128To512(d[0..128])
		field[256] v = sha256compression(u[0..256],u[256..512])
		//then concat the results msb first
		t = CONCAT216x216(v[40..256], s[40..256])
		//and hash the result
		q = shaPad432To512(t)
		s = sha256compression(q[0..256],q[256..512]) //zc

		//concat 1st through 31st siblings
		{t = ORDEREDCONCAT216x216(orderC[*], s[40..256], c*b[40..256])
		u = shaPad432To512(t)
		s = sha256compression(u[0..256],u[256..512])}31...1

		// concat with the 32nd sibling. This should equal the root.
		t = ORDEREDCONCAT216x216(orderC[0], s[40..256], c0b[40..256])
		u = shaPad432To512(t)
		field[256] rc = sha256compression(u[0..256],u[256..512])

	// ***Next calculate Zd = H(D|PkA|S)

		d = CONCAT128x216x216(D, PkA[40..256], Sd[40..256])
		//this is too big to handle in a single sha round and our sha can't handle multiple rounds
		//as a work-around, we will split d, hash both parts and then hash the result - remember to do this in Node too!
		//first 432 bits (432 is 216*2 - a convenient number in our 216 bit scheme)
		q = shaPad432To512(d[128..560])
		s = sha256compression(q[0..256],q[256..512])
		//remaining 128 bits
		u = shaPad128To512(d[0..128])
		v = sha256compression(u[0..256],u[256..512])
		//then concat the results msb first
		t = CONCAT216x216(v[40..256], s[40..256])
		//and hash the result
		q = shaPad432To512(t)
		s = sha256compression(q[0..256],q[256..512]) //zd

	//concat 1st through 31st siblings

		{t = ORDEREDCONCAT216x216(orderD[*], s[40..256], d*b[40..256])
		u = shaPad432To512(t)
		s = sha256compression(u[0..256],u[256..512])}31...1

	// concat with the 32nd sibling. This should equal the root.

		t = ORDEREDCONCAT216x216(orderD[0], s[40..256], d0b[40..256])
		u = shaPad432To512(t)
		field[256] rd = sha256compression(u[0..256],u[256..512])


//*************Next up - prove that E is in Ze and F is in Zf

	//work out ze = H(E|PkB|Se)

		d = CONCAT128x216x216(E, PkB[40..256], Se[40..256])
		//this is too big to handle in a single sha round and our sha can't handle multiple rounds
		//as a work-around, we will split d, hash both parts and then hash the result - remember to do this in Node too!
		//first 432 bits (432 is 216*2 - a convenient number in our 216 bit scheme)
		q = shaPad432To512(d[128..560])
		s = sha256compression(q[0..256],q[256..512])
		//remaining 128 bits
		u = shaPad128To512(d[0..128])
		v = sha256compression(u[0..256],u[256..512])
		//then concat the results msb first
		t = CONCAT216x216(v[40..256], s[40..256])
		//and hash the result
		q = shaPad432To512(t)
		field[256] ze = sha256compression(q[0..256],q[256..512]) //ze

	//and zf

		d = CONCAT128x216x216(F, PkA[40..256], Sf[40..256])
		//this is too big to handle in a single sha round and our sha can't handle multiple rounds
		//as a work-around, we will split d, hash both parts and then hash the result - remember to do this in Node too!
		//first 432 bits (432 is 216*2 - a convenient number in our 216 bit scheme)
		q = shaPad432To512(d[128..560])
		s = sha256compression(q[0..256],q[256..512])
		//remaining 128 bits
		u = shaPad128To512(d[0..128])
		v = sha256compression(u[0..256],u[256..512])
		//then concat the results msb first
		t = CONCAT216x216(v[40..256], s[40..256])
		//and hash the result
		q = shaPad432To512(t)
		field[256] zf = sha256compression(q[0..256],q[256..512]) //zf

//*************sum check C+D = E+F - we don't want anyone to create money.

	field sumIn = pC + pD
	field sumOut = pE + pF

//**************Now test the proofs

Nd[40..256] == nd[40..256]
Nc[40..256] == nc[40..256]
R[40..256] == rd[40..256]
R[40..256] == rc[40..256]
Ze[40..256] == ze[40..256]
Zf[40..256] == zf[40..256]
sumIn == sumOut
C[0] == 0 //overflow prevention
D[0] == 0
E[0] == 0
F[0] == 0
return 1
