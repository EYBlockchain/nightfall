import "hashes/sha256/512bit.code" as sha256compression
import "utils/pack/unpack128.code" as unpack128

def CONCAT216x216x216(field[216] a, field[216] b, field[216] c)->(field[648]):
	return [...a, ...b, ...c]

def CONCAT216x216(field[216] a, field[216] b)->(field[432]):
	return [...a, ...b]

def shaPad432To512(field[432] a)->(field[512]):
	return [...a, 1, 70....0, 1,1,0,1,1,0,0,0,0]

def shaPad216To512(field[216] a)->(field[512]):
	return [...a, 1, 287....0, 1,1,0,1,1,0,0,0]

//inputs for main are:
// Proving Asset Token A is in the token Z { Z = H(A|Pk|S) }
// - S is the 216 bit private token serial number
// - Pk is the 216 bit public key of A derived by hashing the Secret Key Sk of A
// - Z is the 216 bit public identifier of the token
// - A is the 256 bit private asset hash created from swarm (note: it looks like 512 bits are actually being passed as A)

// Note - We truncate the 256 bit SHA hash down to 216 bits to make the code shorter.  This will have an effect on security and
// but not for any practical purpose we believe.

//Each field element passed to main() can be at most 128bits, to make use of unpack128()

def main('field' pA1...0, 'field' £pPk1...0, 'field' £pS1...0, 'field' pZ1...0)->(field1):

	//Unpack the inputs of main() to 128 bits. We'll unpack each field to its own 128 bit string for simplicity for now. Later efficiencies could be made by grouping some inputs.

	field[128] A1 = unpack128(pA1) //this may seem long-winded but ZoKrates needs intermediate variables for now
	field[128] A0 = unpack128(pA0)
	field[256] A = [...A1, ...A0]
	field[128] S1 = unpack128(pS1)
	field[128] S0 = unpack128(pS0)
	field[256] S = [...S1, ...S0]
	field[128] Pk1 = unpack128(pPk1)
	field[128] Pk0 = unpack128(pPk0)
	field[256] Pk = [...Pk1, ...Pk0]
	field[128] Z1 = unpack128(pZ1)
	field[128] Z0 = unpack128(pZ0)
	field[256] Z = [...Z1, ...Z0]


	// compute z = H(A|Pk|S) (sort of). The padding needs to be added to create a true hash

	field[648] d = CONCAT216x216x216(A[40..256], Pk[40..256], S[40..256]) //keep the lowest bytes

	//this is too big to handle in a single sha round and our sha can't handle multiple rounds
	//as a work-around, we will split d, hash both parts and then hash the result - remember to do this in Node too!

		//first 432 bits (432 is 216*2 - a convenient number in our 216 bit scheme)
	field[512] t = shaPad432To512(d[216..648]) //note that we take the low bits first
	field[256] s = sha256compression(t[0..256],t[256..512])
		//remaining 216 high bits
	field[512]u = shaPad216To512(d[0..216])
	field[256] v = sha256compression(u[0..256],u[256..512])
		//then concat the results msb first
	field[432] e = CONCAT216x216(v[40..256], s[40..256])
		//and hash the result
	t = shaPad432To512(e)
	field[256] z = sha256compression(t[0..256],t[256..512])

	//This checks if the hash z we computed using A which is H(A|Pk|S) is equal to Z until the 1st 216 bits

	z[40..256] == Z[40..256]

	return 1
